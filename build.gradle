buildscript {
    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
    ext.sonatypeUrl = "https://repo.maven.apache.org/"
    ext.sonatypeUser = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
    ext.sonatypePassword = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
}

allprojects {

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "checkstyle"
    apply plugin: "signing"

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
        testImplementation "org.assertj:assertj-core:3.24.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"
    }

    checkstyle {
        toolVersion = "8.17"
        maxWarnings = 0
        maxErrors = 0
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    tasks.register('printJars') {
        doLast {
            Set<String> printed = new HashSet<>()
            configurations.each {
                if (it.canBeResolved) {
                    it.files.each {
                        if (!printed.contains(it)) {
                            println it
                            printed.add(it)
                        }
                    }
                }
            }
        }
    }

    test {
        useJUnitPlatform()

        maxParallelForks = Runtime.runtime.availableProcessors()

//        reports {
//            junitXml.enabled = false
//            html.enabled = true
//        }

        testLogging {
            showStandardStreams = project.hasProperty("show-logs")
            events 'PASSED', 'FAILED', 'SKIPPED'
        }

        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }

    tasks.register('sourcesJar', Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    tasks.register('javadocJar', Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {

        repositories {
            maven {
                def releasesRepoUrl = "$sonatypeUrl/maven2"
                def snapshotsRepoUrl = "$sonatypeUrl/maven2"
                url isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
                credentials {
                    username = sonatypeUser
                    password = sonatypePassword
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                groupId 'com.mylaesoftware'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'Config Composer'
                    description = 'A library to auto generate code for Lightbend/Typesafe configuration handling'
                    url = 'https://github.com/claudio-scandura/config-composer'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/claudio-scandura/config-composer/blob/master/LICENSE.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'claudio-scandura'
                            name = 'Claudio Scandura'
                            email = 'cl.scandura@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/claudio-scandura/config-composer.git'
                        developerConnection = 'scm:git:ssh://github.com/claudio-scandura/config-composer.git'
                        url = 'https://github.com/claudio-scandura/config-composer'
                    }
                }
            }
        }
    }

    tasks.withType(Sign).configureEach {
        onlyIf { isReleaseVersion }
    }

    signing {
        sign publishing.publications.mavenJava
        sign configurations.archives
    }

}


wrapper {
    gradleVersion = '8.5'
}
